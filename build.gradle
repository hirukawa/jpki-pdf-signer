apply plugin: 'java'

version = '1.0.1'

tasks.withType(JavaCompile) {
	sourceCompatibility = 11
	targetCompatibility = 11
	options.compilerArgs << '-Xlint:all'
}

repositories {
	jcenter()
}

dependencies {
	implementation fileTree(dir: 'lib',
			includes: ['**/*.jar'],
			excludes: ['**/*-sources.jar', '**/*-javadoc.jar'])

	implementation 'org.apache.pdfbox:pdfbox:2.0.18'
	implementation 'org.apache.pdfbox:fontbox:2.0.18'

	implementation 'com.fasterxml.jackson.core:jackson-databind:2.10.1'

	implementation 'org.kordamp.ikonli:ikonli-javafx:11.3.4'
	implementation 'org.kordamp.ikonli:ikonli-materialdesign-pack:11.3.4'

	// jpki-wrapper dependencies
	runtimeOnly 'org.bouncycastle:bcpkix-jdk15on:1.64'
	runtimeOnly 'net.java.dev.jna:jna:5.5.0'
	runtimeOnly 'net.java.dev.jna:jna-platform:5.5.0'
}

sourceSets.main.resources {
	srcDirs = ['src/main/resources', 'src/main/java' ]
}

def defaultEncoding = 'UTF-8'
tasks.withType(AbstractCompile).each { it.options.encoding = defaultEncoding }
tasks.withType(GroovyCompile).each { it.groovyOptions.encoding = defaultEncoding }

defaultTasks 'clean', 'build', 'exewrap_x64'

jar {
	manifest {
		attributes "Specification-Version": "${project.version}"
		attributes "Main-Class" : "net.osdn.jpki.pdf_signer.Main"
		attributes "Splashscreen-Image": "img/splash.png"
		attributes "Class-Path": sourceSets.main.runtimeClasspath.collect { "lib/$it.name" }.join(' ')
	}
	into('net/osdn/jpki/pdf_signer') {
		from("${projectDir}") {
			include('LICENSE.txt')
		}
	}
}

task copyLibraries(type: Copy) {
	setGroup("build")
	setDescription("Copy dependency libraries.")
	from (sourceSets.main.runtimeClasspath) {
		include '*.jar'
	}
	from('lib') {
		include '*.dll'
	}
	into "${buildDir}/package/lib/"
}

task createJavaRuntime(type: Exec, dependsOn: ['jar', 'copyLibraries']) {
	setGroup("build")
	setDescription("Create Java Runtime.")
	workingDir "${buildDir}/package/"
	commandLine 'cmd', '/c', 'CreateJRE.bat', "${jar.archiveFile.get()}", "lib", "-client"
}

task exewrap_x64(type: Exec, dependsOn: 'jar') {
	setGroup("build")
	setDescription('Assembles the executable.')
	executable "${projectDir}/exewrap.exe"
	args "-A", "x64",
			"-g",
			"-t", "11",
			"-a", "-XX:TieredStopAtLevel=1 -XX:+UseSerialGC -XX:CICompilerCount=1 -Xverify:none -Xms128m",
			"-e", "SHARED",
			"-j", "${jar.archiveFile.get()}",
			"-i", "etc/ico/app.ico",
			"-o", "${buildDir}/package/${archivesBaseName}.exe",
			"-p", "JPKI PDF SIGNER",
			"-d", "JPKI PDF SIGNER",
			"-c", "(C) 2017-2020 HIRUKAWA Ryo",
			"-V", "${version}",
			"-v", "${version}"
}
